/**
 * Selenide / drivers dependencies...
 */
dependencies {
  testImplementation("com.codeborne:selenide:$selenideVersion") {
    exclude module: "selenium-opera-driver"
    exclude module: "selenium-safari-driver"
    exclude module: "selenium-edge-driver"
    exclude module: "selenium-ie-driver"
    because "we don't wanna use any of these..."
  }
  testImplementation("com.codeborne:phantomjsdriver:$phantomjsDriverVersion")
  ////these are must be included by selenide library as a transitive dependencies:
  //testImplementation("org.seleniumhq.selenium:selenium-chrome-driver:$seleniumDriverVersion")
  //testImplementation("org.seleniumhq.selenium:selenium-firefox-driver:$seleniumDriverVersion")
}

static def findAllSelenideSystemProps() {
  System.properties?.findAll { it.key.startsWith("selenide") } ?: [:]
}

final String selenideBrowser = "selenide.browser"
final String defaultBrowser = "chrome" // "phantomjs"

/**
 * Specify default phantomjs headless browser if no one was specified
 *
 * just for default `./gradlew build` execution
 */
test {
  systemProperties = findAllSelenideSystemProps() + [
      "selenide.browser": System.getProperty(selenideBrowser, defaultBrowser)
  ]
}

/**
 * Browser selector tasks: specify / select browser you wanna run within e2e selenide tests
 *
 * Usage:
 *
 * ./gradlew test chrome
 * ./gradlew firefox check
 */
["firefox", "chrome", "phantomjs"].each { taskName ->
  Task e2eTestBrowserSelectorTask = tasks.create(name: taskName) {
    def systemProps = findAllSelenideSystemProps()
    systemProps["$selenideBrowser"] = taskName

    doFirst {
      test {
        systemProperties = systemProps
      }
    }
  }
  test.shouldRunAfter(e2eTestBrowserSelectorTask)
}

final String selenideHeadless = "selenide.headless"

/**
 * Force tests execution in headless mode.
 *
 * ./gradlew test headless chrome
 */
task headless {
  def systemProps = findAllSelenideSystemProps()
  systemProps["$selenideHeadless"] = true

  doFirst {
    test {
      systemProperties = systemProps
    }
  }
}

/**
 * Disabling headless mode even with default system configuration.
 *
 * ./gradlew test headful chrome
 */
task headful {
  def systemProps = findAllSelenideSystemProps()
  systemProps["$selenideHeadless"] = false

  doFirst {
    test {
      systemProperties = systemProps
    }
  }
}
